group 'org.team401'
version '1.1.5'

buildscript {
    ext.kotlin_version = '1.1.0'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url "http://first.wpi.edu/FRC/roborio/maven/release/"
    }

    maven {
        url "http://dev.imjac.in/maven/"
    }
}

configurations {
    required
    compile.extendsFrom required
}

dependencies {
    required "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    required group: 'edu.wpi.first.wpilibj', name: 'athena', version: '2017.3.1'
    required group: 'thirdparty.frc.ctre', name: 'Toolsuite-Java', version: '5.0.2.1'

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.2'
    compile group: 'org.zeromq', name: 'jeromq', version: '0.4.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.required).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    configurations = ['archives']
    pkg {
        repo = "SnakeSkin"
        name = "SnakeSkin"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/snakeskin"
        version {
            name = this.version
        }
    }
}