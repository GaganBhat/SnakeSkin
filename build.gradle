plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.0"
    id "com.palantir.git-version" version "0.10.0"
    id "com.jfrog.bintray" version "1.8.0"
}

group 'org.team401'
version gitVersion()

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    //WPI Maven
    maven {
        url "http://first.wpi.edu/FRC/roborio/maven/release/"
    }
}

configurations {
    required //Represents a dependency that must be provided by the applications using the library
    compile.extendsFrom required
}

dependencies {
    required "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    required group: 'edu.wpi.first.wpilibj', name: 'athena', version: '2017.3.1'

    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'io.github.lukehutch', name: 'fast-classpath-scanner', version: '2.9.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    dependsOn configurations.runtime
    exclude "com/ctre"
    from {
        (configurations.runtime - configurations.required).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

bintray {
    user = project.hasProperty('user') ? project.property('user') : System.getenv("BINTRAY_USER") != null ? System.getenv("BINTRAY_USER") : ""
    key = project.hasProperty('key') ? project.property('key'): System.getenv("BINTRAY_KEY") != null ? System.getenv("BINTRAY_KEY") : ""
    configurations = ['archives']
    pkg {
        repo = "SnakeSkin"
        name = "SnakeSkin"
        userOrg = "team401"
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/team401/snakeskin"
        version {
            name = this.version
        }
    }
}