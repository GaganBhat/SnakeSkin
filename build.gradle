plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.0"
    id "com.palantir.git-version" version "0.10.0"
    id "com.jfrog.bintray" version "1.8.0"
}

group 'org.snakeskin'

jar.enabled = false

allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    group 'org.snakeskin'
    version gitVersion()

    repositories {
        mavenCentral()

        //WPI Maven
        maven {
            url "http://first.wpi.edu/FRC/roborio/maven/release/"
        }
        //401 ThirdParty
        maven {
            url "https://dl.bintray.com/team401/ThirdParty"
        }
    }

    configurations {
        required //Represents a dependency that must be provided by the applications using the library
        compile.extendsFrom required
    }

    dependencies {
        required "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    jar {
        dependsOn configurations.runtime
        destinationDir = file("$rootDir/out/libs")
        from {
            (configurations.runtime - configurations.required).collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

task deployAll() {

}